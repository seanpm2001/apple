-- a_n, b_n, c_n, d_n in Kuhl+Giardina
λxs.λys.λn.
  { sum ← [(+)/1 0 x]
  ; tieSelf ← [({.x)⊳x]; Δ ← [(-)\~(tieSelf x)]
  ; dxs ⟜ Δ xs; dys ⟜ Δ ys
  ; dts ⟜ [√(x^2+y^2)]`dxs dys
  ; pxs ← (+)Λ dxs; pys ← (+)Λ dys; pts ⟜ (+)Λ dts
  ; dtss ⟜ (-)\~((^2)'1 (0<|pts))
  ; dxss ← {:((%)`dxs dts); dyss ← {:((%)`dys dts)
  ; T ⟜}. pts
  ; scaleRad ← [2*n*𝜋*x%T]
  ; c ⟜ T%(2*n^2*𝜋^2)
  ; cosDiffs ⟜ (-)\~([cos.(scaleRad x)]'1 dtss)
  ; sinDiffs ⟜ (-)\~([sin.(scaleRad x)]'1 dtss)
  ; aₙ ← c*sum ((*)`dxss cosDiffs)
  ; bₙ ← c*sum ((*)`dxss sinDiffs)
  ; cₙ ← c*sum ((*)`dyss cosDiffs)
  ; dₙ ← c*sum ((*)`dyss sinDiffs)
  ; (aₙ,bₙ,cₙ,dₙ)
  }
